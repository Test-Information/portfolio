:: **************************************************************************************************************
::  <説明>
::  自宅環境でのOracle学習中に作成したログイン用バッチです。
::  特定の業務に依存しない共通的な処理のため、ポートフォリオ化しようと考えました。
::  以下の処理は実務に使用できるなら使用していただいて構いません。
::  ただし、ポートフォリオ用なので十分なテストは行えておらず、動作の保証はできません。
::
::  <作成情報>
::  作成者  ：神部 慶太
::  作成日  ：2025/12/28
::  開発期間：約1日(初回アップロード時点)
::  
::  <使い方>
::  1)起動パラメータなしで起動した場合、選択メニューを表示し、選択されたユーザーでログインする。
::  2)起動パラメータありで起動した場合、パラメータに対応したユーザーでログインする。
::  
::  <工夫した点>
::  1)ログイン失敗時、sqlplus -L により処理を再試行させず、処理待ち時間の短縮を行う工夫を実施。
::  2)set localにより環境変数をローカル環境変数に限定する工夫を実施。
::  3)変数名や常数を意図的にダブルクォーティングすることで入力値なしの動作にも対応。
::  4)ログイン用のパスワードをハードコーディングしているのは学習用の環境のため、意図的です。
::    実際の実務で使用する場合は、パスワードをなしにして実行すれば、パスワード入力画面が表示され、入力を求められます。
:: **************************************************************************************************************

::コマンド非表示処理
@echo off

::ローカル環境変数設定処理
setlocal

::画面クリア処理
cls

:: *******************************************************
::  バージョン設定
:: *******************************************************
::プログラムバージョン
set "ver=0.0.1"

:: *******************************************************
::  ログインパラメータ設定
:: *******************************************************
::接続ホスト
set "dbHosts=@localhost:1521/ORCLPDB"

::接続ユーザー
set "user001=TEST_SCM"
set "user002=test01"
set "user003=test02"
set "user004=user1"

::接続パスワード
set "pass1=orcl"
set "pass2=pass"
set "pass3=111"
set "pass4=oracle"
set "pass5=password"

::選択メニュー表示処理
echo *******************************************************
echo Oracle DB ログインユーザー選択バッチ(ver:%ver%)
echo =======================================================
echo 0. sysdba    CDBログイン用（管理者ユーザ）
echo 1. sys       PDBログイン用（管理者ユーザ）
echo 2. %user001%
echo 3. %user002%
echo 4. %user003%
echo 5. %user004%
echo *******************************************************

::起動時パラメータ格納処理
set "param=%~1"

:loop
if "%param%" == "" (
	::起動時パラメータなし
	set /p "param=オペレータを選択してください。(半角数字)："
) else (
	echo ユーザー選択【%param%】 自動ログイン実行中...
)

:: *******************************************************
::  ログイン処理
:: *******************************************************
if "%param%"=="0" (
	echo alter session set container=orclpdb | clip
	echo PDBセッション遷移用コマンドをコピーしました。
	::システムユーザログイン処理
	sqlplus / as sysdba
) else if "%param%"=="1" (
	::システムユーザログイン処理（PDB用）
	sqlplus -L sys/%pass1%%dbHosts% as sysdba
) else if "%param%"=="2" (
	::一般ログイン処理（PDB用）
	sqlplus -L %user001%/%pass2%%dbHosts%
) else if "%param%"=="3" (
	::一般ユーザログイン処理（PDB用）
	sqlplus -L %user002%/%pass3%%dbHosts%
) else if "%param%"=="4" (
	::一般ユーザログイン処理（PDB用）
	sqlplus -L %user003%/%pass4%%dbHosts%
) else if "%param%"=="5" (
	::一般ユーザログイン処理（PDB用）
	sqlplus -L %user004%/%pass5%%dbHosts%
) else (
	::入力値不正時
	echo 入力値が不正です。
	::入力パラメータ クリア処理
	set "param="
	::入力待ち再遷移処理
	goto :loop
)

:: *******************************************************
::  終了処理
:: *******************************************************

if errorlevel 1 (
	echo ログインユーザ：%param%
	echo 異常終了コード：%errorlevel%
)
::ローカル環境変数設定終了
endlocal

::コマンドプロンプト切替処理
pause > nul
rem cmd /k
