@echo off
:: ==============================================================================================================
::  <説明>
::  以前、業務で効率化のために作成したバッチを記憶を頼りにポートフォリオ用に再現しました。
::  エクスプローラーの通常機能でもファイルの検索はできますが、以下のような違いがあります。
::  メリット　：CUIによる検索のため、GUI（エクスプローラー）と比べ処理が３０倍近く早い
::  デメリット：エクスプローラーでの検索と違いファイルの中身を検索することはできない
::  
::  品質に問題がないことを確認したつもりではありますが、念のため実行前にコードチェックのうえ実行していただけますと幸いです。
::
::  <作成情報>
::  作成者  ：神部 慶太
::  作成日  ：2025/12/29
::  開発期間：約1日(初回アップロード時点)
::  
::  <使い方>
::  1)起動パラメータありで起動した場合、パラメータで指定されたフォルダに対して検索を行う
::  2)起動パラメータなしで起動した場合、カレントディレクトリに対して検索を行う
:: ==============================================================================================================



::--ローカル環境変数設定--
setlocal
::--プログラムバージョン--
set "ver=1.0.0"

:: ===========================================================================================
::  3.1 パラメータ取得処理
:: ===========================================================================================
if "%~1"=="" (
	::第一パラメータが空の場合
	set "dir=%~dp0" & rem デフォルト検索フォルダ設定処理
) else (
	::第一パラメータが空でない場合
	set "dir=%~1"   & rem 第一パラメータ設定処理
)

if "%dir:~-1%"=="\" (
	::検索ディレクトリの末尾が￥の場合
	set "dir=%dir:~0,-1%"
)

:: ===========================================================================================
::  3.2 フォルダ存在チェック
:: ===========================================================================================
if exist "%dir%\" goto :dir_exist
echo *****************************************************************************************
echo * フォルダが存在しません。%dir%
echo * 第一パラメータに半角スペースが含まれる場合、ダブルクォーティングが必要です。
echo * 処理を終了します。
echo *****************************************************************************************
pause > nul
exit /b

:dir_exist
:reInput
:: ===========================================================================================
::  3.3 入力待機処理
:: ===========================================================================================
echo *****************************************************************************************
echo * 【ファイル名検索バッチ】（ver:%ver%）
echo * 
echo * 検索を開始します。
echo * 検索する単語を入力してください。
echo * 検索対象フォルダ：%dir%
echo * 検索の途中で処理を止める場合、Ctrl+Cを押してください。
echo *****************************************************************************************

set "input="
:err
if "%input%" == "" (
	::入力値が空の場合
	set /p "input="
)

:: ===========================================================================================
::  3.4 入力値チェック
:: ===========================================================================================
if not defined input (
	:: 入力値が定義されていなかった場合
	echo 入力値が不正です。正しい値を入力してください。
	set "input="
	goto :err
)

:: ===========================================================================================
::  3.5 検索処理
:: ===========================================================================================
where /R "%dir%" *"%input%"*

:: ===========================================================================================
::  3.6 検索終了処理
:: ===========================================================================================
if %errorlevel% equ 0 goto :noErr
if %errorlevel% equ 1 goto :noErr
if %errorlevel% equ 2 (
	echo *****************************************************************************************
	echo * 検索が異常終了しました。
	echo * 対象ディレクトリ　：%dir%
	echo * 検索ワード　　　　：%input%
	echo * 異常終了コード　　：%errorlevel%
	echo *****************************************************************************************
	pause
	exit /b
)
:noErr
echo *****************************************************************************************
echo * 検索が終了しました。
echo * もう一度、検索する場合はEnterを押してください。
echo * 終了する場合、ウィンドウ右上の×を押してください。
echo *****************************************************************************************

::-- 一時停止処理（メッセージ表示なし）--
pause > nul

::--クリアスクリーン--
cls

::--ラベル位置にジャンプ--
goto :reInput

::--ローカル環境変数設定終了--
endlocal

::--コマンドプロンプト切替処理--
::cmd /k

